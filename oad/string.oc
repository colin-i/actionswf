
format elfobj64

override include_sec 1
include "common.h" "common.h.oh"

import "re_alloc" re_alloc

importx "malloc" malloc
importx "printf" printf
importx "free" free

#multithread unsafe
char indents=0
#

#                            # is xlog_new_size
function string_add(sv pf,sv p_string_off,valueu strings_base,charx type)
	sv pto;sd psize
	if type=(xlog_new)
		set pto #pf#:preframe.strings
		set psize #pf#:preframe.size
	else #xlog_unique
		set pto #pf#:preframe.ustrings
		set psize #pf#:preframe.usize
	end
	charx ok#1
	set ok re_alloc(pto,psize#)
	if ok=(EXIT_SUCCESS)
		valueu str#1
		sv dest;set dest pto#
		add dest psize#
		if type=(xlog_new)
			set str malloc((\string\))
			if str!=(NULL)
				valueu branches#1
				set branches malloc((sprites_realloc_onepart))
				if branches=(NULL)
					call free(str)
					return (EXIT_FAILURE)
				end
				set str#:string.branches branches

				set str#:string.base strings_base  ##in two steps for log.
				set str#:string.off p_string_off#
				set dest# str
				add psize# :
			end
		else
			set str malloc((\ustring\))
			if str!=(NULL)
				set str#:ustring.base strings_base  ##in two steps for log.
				set str#:ustring.off p_string_off#
				set dest# str
				add psize# :
			end
		end
	end
	return ok
end

function indent()
	charx i#1
	set i global.indents
	while i>0
		call printf("\t")
		dec i
	end
end

function print_indent()
	inc global.indents
end
function print_unindent()
	dec global.indents
end
function print(ss format,valueu value)
	call indent()
	call printf(format,value)
	call print_inline_done()
end
function prints(ss format)
	call indent()
	call printf(format)
	call print_inline_done()
end
function print_inline_start(ss str)
	call indent()
	call printf(str)
end
function print_inline_start_value(ss format,valueu value)
	call indent()
	call printf(format,value)
end
function print_inline(ss format,valueu value)
	call printf(" ")
	call printf(format,value)
end
function print_inline2(ss format,valueu value,valueu v2)
	call printf(" ")
	call printf(format,value,v2)
end
function print_inline_done()
	call printf("\n")
end

function string_log(valueu base,valueu off)
	add base off
	inc off ##to log like in .x where 0 is NULL and 1 is offset 0
	call print_inline2("%s=%lX",base,off) #will recognize the branch (visual) when having the string -> branch link and the branch offset
end

entryraw global()
