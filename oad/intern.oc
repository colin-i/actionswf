
format elfobj64

override include_sec 1
include "common.h" "common.h.oh"

importx "fopen" fopen
importx "fseek" fseek
importx "ftell" ftell
importx "malloc" malloc
importx "fread" fread
importx "fclose" fclose

#multithread unsafe
dataxu xoffset#1
charx blog#1
#

function global_blog()
	return global.blog
end

function fcontent(sd path,sv psize)
	sd f;set f fopen(path,"rb")
	if f!=(NULL)
		call fseek(f,0,(SEEK_END))
		sd size;set size ftell(f)
		if size!=-1
			call fseek(f,0,(SEEK_SET))
			sd mem;set mem malloc(size)
			if mem!=(NULL)
				call fread(mem,size,1,f)
				call fclose(f)
				set psize# size
				return mem
			end
		end
		call fclose(f)
	end
	return (NULL)
end

#at these memnl the last row is ending in nl from src
function memnlcr(ss log,sv pnext,sv _cr) #here is moving through text, an example with cr: ffdec action output is \r\n
	set pnext# 1
	if log#!=(NewLine) #empty row / last row
		whiletrue
			if log#=(NewLine)
				add pnext# log
				dec log
				if log#!=(CReturn)
					inc log
				end
				break
			end
			if log#=(CReturn)
				add pnext# log
				ss next;set next pnext#
				if next#=(NewLine)
					add pnext# 1
					if _cr!=(NULL)
						add _cr# 1
					end
				end
				break
			end
			inc log
		end
	else
		add pnext# log
	end
	return log
end
function memnl(ss log,sv pnext) #here is moving only on nl from src
	set pnext# 1
	if log#!=(NewLine) #emptyrow
		inc log
		while log#!=(NewLine)
			inc log
		end
		add pnext# log
	else
		add pnext# log
	end
	return log
end
function memdec(ss start,value end)
	sub end start
	add start end
	sd nr=1
	sd n;sd val=0
	while end>^0
		dec start

		set n start#
		sub n (ZeroChar)
		mult n nr
		add val n

		mult nr 10 #is ok on 32 where sizeof long is 4
		dec end
	end
	return val
end
#end
function memhex_parse_withinc(ssu start,swu out) #arecordheader,function,if,jump are 15/16 bits at max
	inc start
	ss a;set a start
	whiletrue #the action row is ending in new line
		charx c#1;set c a#
		if c<(ZeroChar);break;end
		if c<=(NineChar)
		elseif c<(A);break;end
		if c>(F);break;end
		inc a
	end
	set out# 0
	if a!=start   ##there are many ,, with no size
		ss b;set b a
		wordxu n#1;set n 1
		while start!=b
			dec b
			wordxu nr#1
			set nr b#
			if nr<=(NineChar)
				sub nr (ZeroChar)
			else
				sub nr (A+10)
			end
			mult nr n
			add out# nr
			mult n 0x10
		end
	end
	return a
end

import "branch_add" branch_add

function newbranch(sv px,sv pxx,valueu prefr)
	sd xx;set xx pxx#
	dataxu branchoffset#1;set branchoffset xx#
	charx ok#1;set ok branch_add(prefr,branchoffset,global.xoffset,px#)
	sub branchoffset global.xoffset
	add px# branchoffset
	add global.xoffset branchoffset
	add pxx# (xlog_code_size)
	return ok
end

import "re_alloc" re_alloc

function function(valueu data)
	charx ok#1;set ok re_alloc(#data#:oadata.functions,data#:oadata.functions_size)
	if ok=(EXIT_SUCCESS)
		add data#:oadata.functions_size :
		#here a re_alloc_add maybe
		return (EXIT_SUCCESS)
	end
	return (EXIT_FAILURE)
end

import "frame_actions" frame_actions
import "string_add" string_add
import "string_id" string_id
import "branch_init" branch_init
import "ibranch_add" ibranch_add
import "ibranch_done" ibranch_done
import "tree_entry_ex" tree_entry_ex
import "tree_entry_start" tree_entry_start
import "tree_entry_end" tree_entry_end
import "jumpforward_resolve" jumpforward_resolve
import "jumpbackward_resolve" jumpbackward_resolve

function actionrow(ss inlog,sd end,sv px,sv pxx,valueu prefr,valueu fr,valueu strings_base,valueu data,svu _text)
	ss start;set start inlog
	inc inlog    ##first is a digit
	whiletrue # break} is still with / (also resolves are there) but it is a row with \n end anyway
		charx chr#1
		set chr inlog#
		if chr<(ZeroChar)
			break
		end
		if chr>(NineChar)
			break
		end
		inc inlog
	end
	valueu number_of_lines#1;set number_of_lines memdec(start,inlog)
	charx ok#1
	set ok frame_actions(fr,number_of_lines,inlog,end,_text)
	if ok=(EXIT_SUCCESS)
		datax recalculation#1 #int? 16 to 8 can be almost on full ui32. a push inside an action is limited by maxuint16 totalvalues
			#not outside actionrow? 'a','a' on multiple rows is still on same row
		sd _recalculation
		if global.blog=(TRUE)
			set recalculation 0
			set _recalculation #recalculation
		else
			set _recalculation (NULL)
		end
		while inlog!=end
			wordxu number#1  #4 places
			valueu branch_item#1
			if inlog#=(xlog_act) #action
				set inlog memhex_parse_withinc(inlog,#number) #Length If code >= 0x80, UI16
				if number=0 # code < 0x80
					add prefr#:preframe.size (actionrecordheader_tag_size)
				else
					add prefr#:preframe.size (actionrecordheader_size)
					add prefr#:preframe.size number
					if global.blog=(TRUE)
						if recalculation!=0
							add prefr#:preframe.log_recalculation recalculation
							set recalculation 0
						end
					end
				end
			elseif inlog#=(xlog_string) #string
				inc inlog ## xlog_const8_log or xlog_const16_log

				ss xxcontent;set xxcontent pxx#
				add pxx# (xlog_type_size)
				if xxcontent#!=(xlog_id) # xlog_new xlog_unique , not parsing .x, ok straight in .xx
					set ok string_add(prefr,pxx#,strings_base,xxcontent#,_recalculation,inlog#)
					if ok!=(EXIT_SUCCESS)
						return (EXIT_FAILURE)
					end
					add pxx# (xlog_unique_new_size)
				else #xlog_id
					set ok string_id(prefr,pxx#,_recalculation,inlog#)
					if ok!=(EXIT_SUCCESS)
						return (EXIT_FAILURE)
					end
					add pxx# (xlog_id_size)
				end
				inc inlog
			elseif inlog#=(xlog_jump)
				set inlog memhex_parse_withinc(inlog,#number)   #ActionIf BranchOffset SI16(here 15 bits)  ActionJump BranchOffset SI16(here 15 bits)
				call jumpforward_resolve(prefr,number)
			elseif inlog#=(xlog_branch) #branch start
				set ok newbranch(px,pxx,prefr)
				if ok!=(EXIT_SUCCESS)
					return (EXIT_FAILURE)
				end
				set branch_item prefr#:preframe.branchpointer
				set branch_item#:branch.subbranch branch_init(prefr,branch_item)
				if branch_item#:branch.subbranch=(NULL)
					return (EXIT_FAILURE)
				end
				if global.blog=(TRUE)
					call tree_entry_ex(prefr,(xlog_branch))
				end
				inc inlog
			elseif inlog#=(xlog_innerbranch)
				set ok newbranch(px,pxx,prefr)
				if ok!=(EXIT_SUCCESS)
					return (EXIT_FAILURE)
				end
				set ok ibranch_add(prefr)
				if ok!=(EXIT_SUCCESS)
					return (EXIT_FAILURE)
				end
				if global.blog=(TRUE)
					call tree_entry_start(prefr)
				end
				inc inlog
			elseif inlog#=(xlog_innerbranchend) #innerbranch end
				set ok newbranch(px,pxx,prefr)
				if ok!=(EXIT_SUCCESS)
					return (EXIT_FAILURE)
				end
				call ibranch_done(prefr)
				if global.blog=(TRUE)
					call tree_entry_end(prefr)
				end
				inc inlog
			elseif inlog#=(xlog_function)
				set ok function(data)
				if ok!=(EXIT_SUCCESS)
					return (EXIT_FAILURE)
				end
				set inlog memhex_parse_withinc(inlog,#number) #codeSize UI16
				if global.blog=(TRUE)
					add prefr#:preframe.log_recalculation (actionrecordheader_size+actionpush_type+actionpush_integer+actionrecordheader_tag_size) #adding func dtitles
					#what if there are no func dtitles? the debugger will not work without that at this moment. maybe a to do
				end
			elseif inlog#=(xlog_jumpback) # at loop end , continue. is goot to set next action to be somewhere inside a suprabranch
				set inlog memhex_parse_withinc(inlog,#number)  #ActionJump BranchOffset SI16(here 15 bits)
				call jumpbackward_resolve(prefr,number)
			else #xlog_vbar is for branch loop start mark and then will leave the branch for a subbranch
				set branch_item prefr#:preframe.branchpointer
				set branch_item#:branch.offset_loop prefr#:preframe.size
				inc inlog
			end
		end
		return (EXIT_SUCCESS)
	end
	return (EXIT_FAILURE)
end

function text_after_nlines(ss inlog,valueu linesnr,valueu crs)
	ss next
	while linesnr>^0
		call memnlcr(inlog,#next,crs)
		set inlog next
		dec linesnr
	end
	return inlog
end

import "presprite_init" presprite_init
import "presprite_done" presprite_done
import "get_nframe" get_nframe

function loop(ss inlog,sv x,sd xx,sd logsize,sv ppresprites,sv psprites,sv data,charx log)
	add logsize inlog
	if inlog!=logsize  ##else no action sprites/shows
		set global.blog log
		sd next
		ss p;set p memnl(inlog,#next)
		whiletrue
			wordxu preid#1
			set preid memdec(inlog,p) #[inlog,p) ui16
			set inlog next

			charx ok#1
			set p memnl(inlog,#next)
			if inlog=p
				set inlog next
				# empty: show/done
				set p memnl(inlog,#next)
				if inlog=p ##show ui16
					set ok presprite_init(preid,ppresprites,psprites,(from_show))
					if ok!=(EXIT_SUCCESS)
						return (EXIT_FAILURE)
					end
				else #done, here for sprite, already opened or ignore it (no action/show)
					wordxu id#1
					set id memdec(inlog,p) #[inlog,p) ui16
					set ok presprite_done(psprites#,preid,id,data)
					if ok!=(EXIT_SUCCESS)
						return (EXIT_FAILURE)
					end
				end
				set inlog next
				if inlog=logsize
					return (EXIT_SUCCESS) #exit after show/done. exit 2
				end
				set p memnl(inlog,#next)
			else
				#ainit
				charx ainit#1;set ainit memdec(inlog,p) #[inlog,p) 0/1
				set inlog next

				#linesnr
				set p memnl(inlog,#next)
				valueu linesnr#1;set linesnr memdec(inlog,p)
				set inlog next

				set ok presprite_init(preid,ppresprites,psprites,(from_action),ainit,linesnr)
				if ok!=(EXIT_SUCCESS)
					return (EXIT_FAILURE)
				end

				valueu text#1;set text inlog
				set inlog text_after_nlines(inlog,linesnr,(NULL))

				sv pf;sv f;set f get_nframe(ppresprites#,psprites#,preid,ainit,#pf) #and from sprites since total lines sum, but will be needed at more, anyway

				valueu strings_size#1
				set strings_size x#
				add x (xlog_strings_size)
				valueu strings_base#1;set strings_base x
				add x strings_size
				dataxu xcode_size#1
				set xcode_size x#
				add x (xlog_xcode_size)
				valueu xcode#1;set xcode x
				add x xcode_size
				set global.xoffset 0

				if log=(TRUE)
					add pf#:preframe.log_xsize xcode_size
				end

				#[actions]
				whiletrue
					if inlog=logsize
						set ok branch_add(pf,xcode_size,global.xoffset,xcode)
						if ok=(EXIT_SUCCESS)
							return (EXIT_SUCCESS) #no [more] actions and nothing else. exit 3
						end
						return (EXIT_FAILURE)
					end
					set p memnl(inlog,#next)
					if inlog#=(xlog_actions)
						inc inlog
						set ok actionrow(inlog,p,#xcode,#xx,pf,f,strings_base,data,#text)
						if ok!=(EXIT_SUCCESS)
							return (EXIT_FAILURE)
						end
					else
						set ok branch_add(pf,xcode_size,global.xoffset,xcode)
						if ok!=(EXIT_SUCCESS)
							return (EXIT_FAILURE)
						end
						break #[inlog,p) will continue at preid
					end
					set inlog next   ##was a row with actions
				end
			end
		end
	end
	return (EXIT_SUCCESS) #exit 1
end

importx "strlen" strlen
importx "memcpy" memcpy
importx "free" free

import "compare" compare

char inter_last_extension_data=".last.data";vstr p_inter_last_extension_data^inter_last_extension_data
function inter_last_extension(sv psize)
	set psize# (\global.inter_last_extension_data)
	return global.p_inter_last_extension_data
end
function inter(ss swf_fullpath)
	sd size;set size strlen(swf_fullpath)
	sd size1=\global.inter_last_extension_data
	add size1 size
	sd from;set from malloc(size1)
	if from!=(NULL)
		call memcpy(from,swf_fullpath,size)
		sub size1 size
		add size from
		call memcpy(size,global.p_inter_last_extension_data,size1)
		sd file
		set file fopen(from,"wb")
		call free(from)
		if file!=(NULL)
			call fclose(file)
			sd exit;set exit compare(swf_fullpath)
			return exit
		end
	end
	return (EXIT_FAILURE)
end

function oadata_init(sv poadata)
	set poadata# malloc(\oadata\)
	sv a;set a poadata#
	if a!=(NULL)
		set a#:oadata.sprites malloc((sprites_realloc_onepart))
		if a#:oadata.sprites!=(NULL)
			set a#:oadata.sprites_size 0

			set a#:oadata.functions malloc((sprites_realloc_onepart))
			if a#:oadata.functions!=(NULL)
				set a#:oadata.functions_size 0

				set a#:oadata.marks malloc(\mark\)
				sd m;set m a#:oadata.marks
				if m!=(NULL)
					set m#:mark.prev (mark_nothing)
					set m#:mark.next (mark_nothing)
					return (EXIT_SUCCESS)
				end
			end
		end
	end
	return (EXIT_FAILURE)
end

entryraw global() #this blank (an empty struct). is here only for global variables
