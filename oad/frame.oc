
format elfobj64

importx "realloc" realloc

override include_sec 1
include "common.h" "common.h.oh"

function re_alloc(sv pmem,dataxu size) #only dataxu external sizes, and +1 increments
	dataxu currentmaxsize#1;set currentmaxsize size
	divu currentmaxsize ((sprites_realloc_onepart))
	mult currentmaxsize ((sprites_realloc_onepart))
	if size=currentmaxsize   ##is not on the edge
		add currentmaxsize ((sprites_realloc_onepart))
		sv newmem;set newmem realloc(pmem#,currentmaxsize)
		if newmem!=(NULL)
			set pmem# newmem
			return (EXIT_SUCCESS)
		end
		return (EXIT_FAILURE)
	end
	return (EXIT_SUCCESS)
end
function re_calloc(sv pmem,svu size,valueu newsize)
	charx ok#1;set ok re_alloc(pmem,size)
	if ok=(EXIT_SUCCESS)
		add size pmem#
		add newsize pmem#
		while size!=newsize
			set size# (NULL)
			add size :
		end
	end
	return ok
end
function re_calloc_size(sv pmem,sdu psize,dataxu newsize)
	charx ok#1;set ok re_calloc(pmem,psize#,newsize)
	if ok=(EXIT_SUCCESS)
		set psize# newsize
	end
	return ok
end

importx "malloc" malloc
importx "free" free

import "print_inline_start" print_inline_start
import "print_inline_start_value" print_inline_start_value
import "print_inline" print_inline
import "print_inline_done" print_inline_done
import "print" print
import "print_indent" print_indent
import "print_unindent" print_unindent

function preframe_init()
	sv pf;set pf malloc((\preframe\))
	if pf!=(NULL)
		valueu bra#1
		set bra malloc((\branch\))
		if bra!=(NULL)
			sv strings
			set strings malloc((sprites_realloc_onepart))
			if strings!=(NULL)
				sv ustrings
				set ustrings malloc((sprites_realloc_onepart))
				if ustrings!=(NULL)
					set pf#:preframe.abranch bra
					set pf#:preframe.strings strings
					set pf#:preframe.size 0
					set pf#:preframe.ustrings ustrings
					set pf#:preframe.usize 0
					return pf
				end
				call free(strings)
			end
			call free(bra)
		end
		call free(pf)
	end
	return (NULL)
end
function preframe_free(sv pf,charx log,charx ainit)
	if log=(TRUE)
		call frame_print_top(ainit)
		call print_inline_start("ustrings:")
	end
	call free(pf#:preframe.abranch)
	valueu str#1

	#is a different free than strings, will have one branch vs multiple
	sv ustrings;set ustrings pf#:preframe.ustrings
	sv s;set s ustrings
	sv end;set end pf#:preframe.usize
	add end s
	while s!=end
		set str s#
		if log=(TRUE)
			call print_inline("%s",str#:ustring.text)
		end
		call free(str)
		incst s
	end
	call free(ustrings)

	if log=(TRUE)
		call print_inline_done()
		call print_inline_start("strings:")
	end

	sv strings;set strings pf#:preframe.strings
	set s strings
	set end pf#:preframe.size
	add end s
	while s!=end
		set str s#
		if log=(TRUE)
			call print_inline("%s",str#:string.text)
		end
		call free(str#:string.branches)
		call free(str)
		incst s
	end
	call free(strings)

	if log=(TRUE)
		call print_inline_done()
		call print_unindent()
	end

	call free(pf)
end

function frame_init(valueu total)
	sv f;set f malloc((\frame\))
	if f!=(NULL)
		valueu size#1;set size (\line\)
		mult size total
		sv lines;set lines malloc(size)
		if lines!=(NULL)
			set f#:frame.lines lines
			set f#:frame.total total
			return f
		end
		call free(f)
	end
	return (NULL)
end
function frame_free(sv f,valueu pointer_is_log,charx ainit)
	valueu end#1;set end f#:frame.total
	valueu ix#1;valueu n#1
	if pointer_is_log!=(NULL)
		call frame_print_top(ainit)
		call print_inline_start_value("lines=%lu",end)
		set ix 0;set n 0
	end
	mult end (\line\)
	sv lines;set lines f#:frame.lines
	sv pointer;set pointer lines
	add end pointer
	while pointer!=end
		if pointer_is_log!=(NULL)
			valueu i#1;set i pointer#:line.index
			if i!=ix
				call print_inline("%u",n)
				set ix i;set n 1
			else
				inc n
			end
		end
		add pointer (\line\)
	end
	if pointer_is_log!=(NULL)
		call print_inline("%u",n)
		call print_inline_done()
		call print_unindent()
	end
	call free(lines)
	call free(f)
end

function frame_print_top(charx ainit)
	if ainit=(xlog_ainit_false)
		call print("frame")
	else
		call print("init")
	end
	call print_indent()
end

function nframe_init(sv pf,valueu total)
	set pf# preframe_init()
	if pf#!=(NULL)
		valueu f#1;set f frame_init(total)
		return f
	end
	return (NULL)
end

function get_nframe(sv presprites,sv sprites,wordxu preid,charx ainit,sv ppf)
	sv ps=:
	mult ps preid
	sv s;set s ps

	add ps presprites
	set ps ps#

	add s sprites
	set s s#

	if ainit=(xlog_ainit_false)
		sv pf
		set pf ps#:presprite.preframes
		add pf ps#:presprite.size
		decst pf
		set ppf# pf#

		sv f
		set f s#:sprite.frames
		add f s#:sprite.size
		decst f
		return f#
	end
	set ppf# ps#:presprite.apreframe
	return s#:sprite.aframe
end

function frame_actions(valueu fr,valueu lines,sv p_totallines)
	valueu current#1;set current p_totallines#
	add p_totallines# lines

	valueu p_lines=\line\
	mult p_lines current
	add p_lines fr#:frame.lines
	mult lines \line\
	add lines p_lines
	while p_lines!=lines
		set p_lines#:line.index current
		add p_lines (\line\)
	end
end
function frame_expand(valueu fr,valueu lines,sv p_totallines)
	add lines fr#:frame.total
	valueu alloc#1;set alloc \line\
	mult alloc lines
	valueu re#1
	set re realloc(fr#:frame.lines,alloc)
	if re!=(NULL)
		set fr#:frame.lines re
		set p_totallines# fr#:frame.total
		set fr#:frame.total lines
		return (EXIT_SUCCESS)
	end
	return (EXIT_FAILURE)
end
function frame_expected(valueu fr,valueu lines,valueu totallines)
	if lines!=totallines
		set fr#:frame.total lines
	end #there is nothing at those last lines, also, was allocated there, ignoring
end
