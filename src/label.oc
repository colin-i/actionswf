
format elfobj64

include "../include/prog.h" "../include/prog.oh"

const label_max=200
const stepsize=DWORD+DWORD

#multithread unsafe
vdataxu gotos#label_max
charx pos#1

import "stack_to_word_arg" stack_to_word_arg
import "swf_actionblock_get_mem_size" swf_actionblock_get_mem_size

function label_resolve(sdu ?label)
	sdu pointer
	set pointer globalg.pos
	mult pointer (stepsize)
	add pointer #globalg.gotos
	sd max;set max pointer
	ss mem;sdu ?size
	call swf_actionblock_get_mem_size(#mem,#size)
	while #globalg.gotos!=pointer
		sub pointer (DWORD)
		sdu ?off;set off pointer#
		sub pointer (DWORD)
		if pointer#=label
			ss a;set a mem
			add a off
			sdu ?sz;set sz size
			sub sz off
			call stack_to_word_arg(sz,a)

			sdu cursor=stepsize
			add cursor pointer
			sdu marker;set marker pointer
			while cursor!=max
				set marker# cursor#
				add marker (DWORD);add cursor (DWORD)
				set marker# cursor#
				add marker (DWORD);add cursor (DWORD)
			end
			sub max (stepsize)
			dec globalg.pos
		end
	end
end

importx "sscanf" sscanf

#aftercallimport ebool

import "error" error

function goto_add(sdu ?label,sdu ?off)
	if globalg.pos=(label_max)
		call error("too many goto")
	end
	sd pointer
	set pointer globalg.pos
	mult pointer (stepsize)
	add pointer #globalg.gotos

	set pointer# label
	add pointer (DWORD)
	set pointer# off

	inc globalg.pos
end

import "action_code_set" action_code_set

function label_add(ss ac)
	sdu ?addres ;#same
	sd ?scan;set scan sscanf(ac,"addr%x",#addres) #same string like at goto
	if scan=1
		call action_code_set((label))
		call action_code_set(addres)
		ret
	end
	call error("label addr sscanf error")
end

entryraw globalg()
